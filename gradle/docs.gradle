configurations {
	asciidoctorExt
}

dependencies {
	asciidoctorExt("io.spring.asciidoctor:spring-asciidoctor-extensions:0.2.0.RELEASE")
}


ext{
	highlightjsdir = "http://docs.jcohy.com/js/highlight"
	highlightjsTheme = "github"
	sourceHighlighter = "highlight.js"
}
task extractDocResources(type: Copy) {
	from "./buildSrc/src/main/resources"
	into "$buildDir/docs/docs-resources"
}

asciidoctorj {
	modules {
		pdf {
			version '1.5.0-beta.8'
		}
	}
}

asciidoctor {
	baseDirFollowsSourceDir()
	configurations 'asciidoctorExt'
	sources {
		include '*.adoc'
	}
	outputDir "$buildDir/docs/ref-docs/"
	resources {
		from(sourceDir) {
			include 'css/**', 'js/**'
		}
		from extractDocResources
	}
	logDocuments = true
	outputOptions {
		backends = ["html5"]
	}
	options doctype: 'book', eruby: 'erubis'
	attributes([
			icons: 'font',
			idprefix: '',
			idseparator: '-',
			docinfo: 'shared',
			'source-highlighter': "${sourceHighlighter}",
			highlightjsdir: "${highlightjsdir}",
			'highlightjs-theme': "${highlightjsTheme}",
			stylesdir: "$buildDir/docs/docs-resources/css",
			stylesheet: 'stylesheet.css',
			revnumber: project.version,
			sectanchors: '',
			sectnums: '',
	])
}

asciidoctor.dependsOn extractDocResources